plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id("maven-publish")
    id("com.jfrog.bintray") version "1.8.5"
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"


    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

ext {
    navigation_version = '2.3.0'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
}

task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.flownav'
                artifactId = 'router'
                version = publish_version
                artifact androidSourcesJar
            }
        }
    }
}

bintray {

    def pUser = ""
    def pKey = ""

    if (hasProperty("user")) pUser = property("user")
    if (hasProperty("key")) pKey = property("key")

    user = pUser
    key = pKey

    setPublications("release")

    pkg {
        name = "Router"
        repo = "FlowNav"
        websiteUrl = "https://github.com/jeziellago/FlowNav"
        issueTrackerUrl = "https://github.com/jeziellago/FlowNav/issues"
        vcsUrl = "https://github.com/jeziellago/FlowNav.git"
        publicDownloadNumbers = true
        setLicenses("Apache-2.0")
        desc = "Android annotation processor and tools for providing better navigation on multi-modules projects."
        version.name = publish_version
        version.vcsTag = publish_version
        publish = true
        override = true
    }
}
