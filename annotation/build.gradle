plugins {
    id("kotlin")
    id("maven-publish")
    id("com.jfrog.bintray") version "1.8.5"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

repositories {
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task sourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from sourceSets.getByName("main").allSource
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.flownav'
            artifactId = 'annotation'
            version = publish_version
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {

    def pUser = ""
    def pKey = ""

    if (hasProperty("user")) pUser = property("user")
    if (hasProperty("key")) pKey = property("key")

    user = pUser
    key = pKey

    setPublications("release")

    pkg {
        name = "Annotation"
        repo = "FlowNav"
        websiteUrl = "https://github.com/jeziellago/FlowNav"
        issueTrackerUrl = "https://github.com/jeziellago/FlowNav/issues"
        vcsUrl = "https://github.com/jeziellago/FlowNav.git"
        publicDownloadNumbers = true
        setLicenses("Apache-2.0")
        desc = "Android annotation processor and tools for providing better navigation on multi-modules projects."
        version.name = publish_version
        version.vcsTag = publish_version
        publish = true
        override = true
    }
}
